buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.squareup.sqldelight:gradle-plugin:0.5.1'
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.13.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.squareup.sqldelight'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "lt.gyvosistorijos"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 3
        versionName "0.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "MAPBOX_ACCESS_TOKEN", "\"" + MAPBOX_ACCESS_TOKEN + "\"")
        resValue("string", "MAPBOX_MAP_STYLE_URL", "\"" + MAPBOX_MAP_STYLE_URL + "\"")
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-debug'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

ext {
    playServicesVersion = '9.8.0'
    retrofitVersion = '2.1.0'
    supportVersion = '25.0.0'
    okHttpVersion = '3.4.2'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:support-annotations:$supportVersion"
    testCompile 'junit:junit:4.12'

    compile('com.mapbox.mapboxsdk:mapbox-android-sdk:4.1.1@aar') {
        transitive = true
    }

    compile "com.squareup.okhttp3:okhttp:$okHttpVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    compile 'com.squareup.moshi:moshi:1.3.1'
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.bluelinelabs:conductor:2.0.4'
    compile 'com.jakewharton.timber:timber:4.3.1'


    compile "com.google.android.gms:play-services-location:$playServicesVersion"

    compile "com.google.firebase:firebase-core:$playServicesVersion"
    compile "com.google.firebase:firebase-messaging:$playServicesVersion"
    compile "com.google.firebase:firebase-config:$playServicesVersion"
    compile "com.google.firebase:firebase-crash:$playServicesVersion"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
}

repositories {
    mavenCentral()
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'